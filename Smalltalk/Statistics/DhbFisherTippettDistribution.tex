$$\halign{ #\hfil&\quad#\hfil\cr {\sl Class}& {\Large\bf DhbFisherTippettDistribution}\cr
{\sl Subclass of }&{\tt DhbProbabilityDensity}\cr\noalign{\vskip 1ex}

{\sl Instance variable names:}&\parbox[t]{4 in}{\tt  alpha beta }\cr\noalign{\vskip 1ex}}$$


Class methods
{\parskip 1ex\par\noindent}
{\bf distributionName}
\begin{verbatim}
    ^'Fisher-Tippett distribution'

\end{verbatim}
{\bf fromHistogram:} {\tt aHistogram}
\begin{verbatim}
    | beta |
    beta := aHistogram standardDeviation.
    beta = 0 ifTrue: [^nil].
    beta := beta * (6 sqrt / Float pi).
    ^self shape: aHistogram average - (0.5772156649 * beta) scale: 
                                                                  beta

\end{verbatim}
{\bf new}
\begin{verbatim}
    ^self shape: 0 scale: 1

\end{verbatim}
{\bf shape:} {\tt aNumber1} {\bf scale:} {\tt aNumber2}
\begin{verbatim}
    ^super new initialize: aNumber1 scale: aNumber2

\end{verbatim}



Instance methods
{\parskip 1ex\par\noindent}
{\bf average}
\begin{verbatim}
    ^0.577256649 * beta + alpha

\end{verbatim}
{\bf changeParametersBy:} {\tt aVector}
\begin{verbatim}
    alpha := alpha + ( aVector at: 1).
    beta := beta + ( aVector at: 2).

\end{verbatim}
{\bf distributionValue:} {\tt aNumber}
\begin{verbatim}
    | arg |
    arg := ( aNumber - alpha) / beta.
    arg := arg < DhbFloatingPointMachine new largestExponentArgument 
                                                               negated
                    ifTrue: [ ^0]
                    ifFalse:[arg negated exp].
    ^arg > DhbFloatingPointMachine new largestExponentArgument 
                                                          ifTrue: [ 1]
                        ifFalse:[ arg negated exp]

\end{verbatim}
{\bf initialize:} {\tt aNumber1} {\bf scale:} {\tt aNumber2}
\begin{verbatim}
    aNumber2 > 0
        ifFalse: [ self error: 'Illegal distribution parameters'].
    alpha := aNumber1.
    beta := aNumber2.
    ^self

\end{verbatim}
{\bf integralFrom:} {\tt aNumber1} {\bf to:} {\tt aNumber2}
\begin{verbatim}
    ^( DhbRombergIntegrator new: self from: aNumber1 to: aNumber2) 
                                                              evaluate

\end{verbatim}
{\bf integralUpTo:} {\tt aNumber}
\begin{verbatim}
    ^( DhbRombergIntegrator new:
            [ :x | x = 0 ifTrue: [ 0] ifFalse: [ ( self value: 1 / x) 
                                                        / x squared] ]
            from: 1 / aNumber to: 0) evaluate

\end{verbatim}
{\bf kurtosis}
\begin{verbatim}
    ^2.4

\end{verbatim}
{\bf parameters}
\begin{verbatim}
    ^Array with: alpha with: beta

\end{verbatim}
{\bf random}
\begin{verbatim}
    | t |
    [ t := DhbMitchellMooreGenerator new floatValue ln negated.
      t > 0] whileFalse: [].
    ^t ln negated * beta + alpha

\end{verbatim}
{\bf skewness}
\begin{verbatim}
    ^1.3

\end{verbatim}
{\bf standardDeviation}
\begin{verbatim}
    ^Float pi * beta / ( 6 sqrt)

\end{verbatim}
{\bf value:} {\tt aNumber}
\begin{verbatim}
    | arg |
    arg := ( aNumber - alpha) / beta.
    arg := arg > DhbFloatingPointMachine new largestExponentArgument 
                                                         ifTrue: [ ^0]
                        ifFalse:[arg negated exp + arg].
    ^arg > DhbFloatingPointMachine new largestExponentArgument 
                                                          ifTrue: [ 0]
                        ifFalse:[ arg negated exp / beta]

\end{verbatim}
{\bf valueAndGradient:} {\tt aNumber}
\begin{verbatim}
    | dp dy y|
    dp := self value: aNumber.
    y := ( aNumber - alpha) / beta.
    dy := ( y negated exp - 1).
    ^Array with: dp
           with: ( DhbVector with: dy * dp / beta negated
                             with: dp * ( y * dy + 1) / beta negated)

\end{verbatim}

