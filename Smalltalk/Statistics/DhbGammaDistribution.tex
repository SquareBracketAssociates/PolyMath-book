$$\halign{ #\hfil&\quad#\hfil\cr {\sl Class}& {\Large\bf DhbGammaDistribution}\cr
{\sl Subclass of }&{\tt DhbProbabilityDensity}\cr\noalign{\vskip 1ex}

{\sl Instance variable names:}&\parbox[t]{4 in}{\tt  alpha beta norm randomCoefficients incompleteGammaFunction }\cr\noalign{\vskip 1ex}}$$


Class methods
{\parskip 1ex\par\noindent}
{\bf distributionName}
\begin{verbatim}
    ^'Gamma distribution'

\end{verbatim}
{\bf fromHistogram:} {\tt aHistogram}
\begin{verbatim}
    | alpha beta |
    aHistogram minimum < 0
        ifTrue: [ ^nil].
    alpha := aHistogram average.
    beta := aHistogram variance / alpha.
    ^[ self shape: alpha / beta scale: beta] when: ExAll do: [ 
                                       :signal | signal exitWith: nil]

\end{verbatim}
{\bf new}
\begin{verbatim}
    ^self error: 'Illegal creation message for this class'

\end{verbatim}
{\bf shape:} {\tt aNumber1} {\bf scale:} {\tt aNumber2}
\begin{verbatim}
    ^super new initialize: aNumber1 scale: aNumber2

\end{verbatim}



Instance methods
{\parskip 1ex\par\noindent}
{\bf average}
\begin{verbatim}
    ^alpha * beta

\end{verbatim}
{\bf changeParametersBy:} {\tt aVector}
\begin{verbatim}
    alpha := alpha + ( aVector at: 1).
    beta := beta + ( aVector at: 2).
    self computeNorm.
    incompleteGammaFunction := nil.
    randomCoefficients := nil.

\end{verbatim}
{\bf computeNorm}
\begin{verbatim}
    norm := beta ln * alpha + alpha logGamma.

\end{verbatim}
{\bf distributionValue:} {\tt aNumber}
\begin{verbatim}
    ^self incompleteGammaFunction value: aNumber / beta

\end{verbatim}
{\bf incompleteGammaFunction}
\begin{verbatim}
    incompleteGammaFunction isNil 
        ifTrue: 
            [incompleteGammaFunction := DhbIncompleteGammaFunction 
                                                        shape: alpha].
    ^incompleteGammaFunction

\end{verbatim}
{\bf initialize:} {\tt aNumber1} {\bf scale:} {\tt aNumber2}
\begin{verbatim}
    ( aNumber1 > 0 and: [ aNumber2 > 0])
        ifFalse: [ self error: 'Illegal distribution parameters'].
    alpha := aNumber1.
    beta := aNumber2.
    self computeNorm.
    ^self

\end{verbatim}
{\bf initializeRandomCoefficientsForLargeAlpha}
\begin{verbatim}
    | a b q d |
    a := 1 / ( 2 * alpha - 1) sqrt.
    b := alpha - (4 ln).
    q := 1 / a + alpha.
    d := 4.5 ln + 1.
    ^Array with: a with: b with: q with: d

\end{verbatim}
{\bf initializeRandomCoefficientsForSmallAlpha}
\begin{verbatim}
    | e |
    e := 1 exp.
    ^( e + alpha) / e

\end{verbatim}
{\bf kurtosis}
\begin{verbatim}
    ^6 / alpha

\end{verbatim}
{\bf parameters}
\begin{verbatim}
    ^Array with: alpha with: beta

\end{verbatim}
{\bf random}
\begin{verbatim}
    ^( alpha > 1 ifTrue: [ self randomForLargeAlpha]
                        ifFalse:[ self randomForSmallAlpha]) * beta

\end{verbatim}
{\bf randomCoefficientsForLargeAlpha}
\begin{verbatim}
    randomCoefficients isNil
        ifTrue: [ randomCoefficients := self 
                           initializeRandomCoefficientsForLargeAlpha].
    ^randomCoefficients

\end{verbatim}
{\bf randomCoefficientsForSmallAlpha}
\begin{verbatim}
    randomCoefficients isNil
        ifTrue: [ randomCoefficients := self 
                           initializeRandomCoefficientsForSmallAlpha].
    ^randomCoefficients

\end{verbatim}
{\bf randomForLargeAlpha}
\begin{verbatim}
    [ true] whileTrue: [
    | u1 u2 c v y z w|
    u1 := DhbMitchellMooreGenerator new floatValue.
    u2 := DhbMitchellMooreGenerator new floatValue.
    c := self randomCoefficientsForLargeAlpha.
    v := ( u1 / ( 1 - u1)) ln * (c at: 1).
    y := v exp * alpha.
    z := u1 squared * u2.
    w := ( c at: 3) * v + ( c at: 2) - y.
    ( c at: 4) + w >= ( 4.5 * z) ifTrue: [ ^y].
    z ln <= w ifTrue: [ ^y].
                                ].

\end{verbatim}
{\bf randomForSmallAlpha}
\begin{verbatim}
    [ true] whileTrue: [
    | p |
    p := DhbMitchellMooreGenerator new floatValue * self 
                                      randomCoefficientsForSmallAlpha.
    p > 1
        ifTrue: [ | y |
                     y := ( ( self randomCoefficientsForSmallAlpha - 
                                               p) / alpha) ln negated.
                     DhbMitchellMooreGenerator new floatValue <= ( y 
                                               raisedTo: ( alpha - 1))
                        ifTrue: [ ^y].
                    ]
        ifFalse: [ | y |
                        y := p raisedTo: ( 1 / alpha).
                     DhbMitchellMooreGenerator new floatValue <= ( y 
                                                          negated exp)
                        ifTrue: [ ^y].
                     ].
                                ].

\end{verbatim}
{\bf skewness}
\begin{verbatim}
    ^2 / alpha sqrt

\end{verbatim}
{\bf value:} {\tt aNumber}
\begin{verbatim}
    ^aNumber > 0
        ifTrue: [ ( aNumber ln * (alpha - 1) - (aNumber / beta) - 
                                                            norm) exp]
        ifFalse:[ 0].

\end{verbatim}
{\bf variance}
\begin{verbatim}
    ^beta squared * alpha

\end{verbatim}

