$$\halign{ #\hfil&\quad#\hfil\cr {\sl Class}& {\Large\bf DhbFisherSnedecorDistribution}\cr
{\sl Subclass of }&{\tt DhbProbabilityDensity}\cr\noalign{\vskip 1ex}

{\sl Instance variable names:}&\parbox[t]{4 in}{\tt  dof1 dof2 norm chiSquareDistribution1 chiSquareDistribution2 incompleteBetaFunction }\cr\noalign{\vskip 1ex}}$$


Class methods
{\parskip 1ex\par\noindent}
{\bf degreeOfFreedom:} {\tt anInteger1} {\bf degreeOfFreedom:} {\tt anInteger2}
\begin{verbatim}
    ^super new initialize: anInteger1 and: anInteger2

\end{verbatim}
{\bf distributionName}
\begin{verbatim}
    ^'Fisher-Snedecor distribution'

\end{verbatim}
{\bf fromHistogram:} {\tt aHistogram}
\begin{verbatim}
    | n1 n2 a |
    aHistogram minimum < 0 ifTrue: [^nil].
    n2 := (2 / (1 - (1 / aHistogram average))) rounded.
    n2 > 0 ifFalse: [^nil].
    a := (n2 - 2) * (n2 - 4) * aHistogram variance / (n2 squared * 
                                                                   2).
    n1 := (0.7 * (n2 - 2) / (1 - a)) rounded.
    ^n1 > 0 
        ifTrue: [self degreeOfFreedom: n1 degreeOfFreedom: n2]
        ifFalse: [nil]

\end{verbatim}
{\bf new}
\begin{verbatim}
    ^self error: 'Illegal creation message for this class'

\end{verbatim}
{\bf test:} {\tt aStatisticalMoment1} {\bf with:} {\tt aStatisticalMoment2}
\begin{verbatim}
    ^(self class degreeOfFreedom: aStatisticalMoment1 count
        degreeOfFreedom: aStatisticalMoment2 count) 
            distributionValue: aStatisticalMoment1 variance 
                    / aStatisticalMoment2 variance

\end{verbatim}



Instance methods
{\parskip 1ex\par\noindent}
{\bf average}
\begin{verbatim}
    ^dof2 > 2
        ifTrue: [ dof2 / ( dof2 - 2)]
        ifFalse:[ nil]

\end{verbatim}
{\bf changeParametersBy:} {\tt aVector}
\begin{verbatim}
    dof1 := ( dof1 + ( aVector at: 1)) max: 1.
    dof2 := ( dof2 + ( aVector at: 2)) max: 1.
    self computeNorm.
    chiSquareDistribution1 := nil.
    chiSquareDistribution2 := nil.
    incompleteBetaFunction := nil.

\end{verbatim}
{\bf computeNorm}
\begin{verbatim}
    norm := ( dof1 ln * ( dof1 / 2) ) + ( dof2 ln * ( dof2 / 2) )
                        - ( ( dof1 / 2) logBeta: ( dof2 / 2) ).

\end{verbatim}
{\bf confidenceLevel:} {\tt aNumber}
\begin{verbatim}
    aNumber < 0
        ifTrue: [ self error: 'Confidence level argument must be 
                                                           positive'].
    ^( ( self distributionValue: aNumber) - ( self distributionValue: 
                                           aNumber reciprocal) ) * 100

\end{verbatim}
{\bf distributionValue:} {\tt aNumber}
\begin{verbatim}
    ^1 - ( self incompleteBetaFunction value: ( dof2 / ( aNumber * 
                                                        dof1 + dof2)))

\end{verbatim}
{\bf incompleteBetaFunction}
\begin{verbatim}
    incompleteBetaFunction isNil 
        ifTrue: 
            [incompleteBetaFunction := DhbIncompleteBetaFunction 
                                                       shape: dof2 / 2
                        shape: dof1 / 2].
    ^incompleteBetaFunction

\end{verbatim}
{\bf initialize:} {\tt anInteger1} {\bf and:} {\tt anInteger2}
\begin{verbatim}
    dof1 := anInteger1.
    dof2 := anInteger2.
    self computeNorm.
    ^self

\end{verbatim}
{\bf parameters}
\begin{verbatim}
    ^Array with: dof1 with: dof2

\end{verbatim}
{\bf random}
\begin{verbatim}
    chiSquareDistribution1 isNil
        ifTrue: [ chiSquareDistribution1 := DhbChiSquareDistribution 
                                                degreeOfFreedom: dof1.
                  chiSquareDistribution2 := DhbChiSquareDistribution 
                                                degreeOfFreedom: dof2.
                ].
    ^chiSquareDistribution1 random * dof2 / ( chiSquareDistribution2 
                                                        random * dof1)

\end{verbatim}
{\bf value:} {\tt aNumber}
\begin{verbatim}
    ^aNumber > 0
        ifTrue: [ ( norm + ( aNumber ln * ( dof1 / 2 - 1) ) - ( 
             (aNumber * dof1 + dof2) ln * ( ( dof1 + dof2) / 2))) exp]
        ifFalse:[ 0]

\end{verbatim}
{\bf variance}
\begin{verbatim}
    ^dof2 > 4 ifTrue: [ dof2 squared * 2 * ( dof1 + dof2 - 2) / ( ( 
                              dof2 - 2) squared * dof1 * ( dof2 - 4))]
                   ifFalse:[ nil]

\end{verbatim}

