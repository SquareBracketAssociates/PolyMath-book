$$\halign{ #\hfil&\quad#\hfil\cr {\sl Class}& {\Large\bf DhbSplineInterpolator}\cr
{\sl Subclass of }&{\tt DhbNewtonInterpolator}\cr\noalign{\vskip 1ex}

{\sl Instance variable names:}&\parbox[t]{4 in}{\tt  startPointDerivative endPointDerivative }\cr\noalign{\vskip 1ex}}$$


Instance methods
{\parskip 1ex\par\noindent}
{\bf computeSecondDerivatives}
\begin{verbatim}
    | size u w s dx inv2dx |
    size := pointCollection size.
    coefficients := Array new: size.
    u := Array new: size - 1.
    startPointDerivative isNil 
        ifTrue: 
            [coefficients at: 1 put: 0.
            u at: 1 put: 0]
        ifFalse: 
            [coefficients at: 1 put: -1 / 2.
            s := 1 / (( self xPointAt: 2) x - ( self xPointAt: 1) x).
            u at: 1
                put: 3 * s 
                        * (s * (( self yPointAt: size) - ( self 
                                                 yPointAt: size - 1)) 
                                - startPointDerivative)].
    2 to: size - 1
        do: 
            [:n | 
            dx := (self xPointAt: n) - (self xPointAt: ( n - 1)).
            inv2dx := 1 / (( self xPointAt: n + 1) - (self xPointAt: 
                                                              n - 1)).
            s := dx * inv2dx.
            w := 1 / (s * (coefficients at: n - 1) + 2).
            coefficients at: n put: (s - 1) * w.
            u at: n
                put: (((( self yPointAt: n + 1) - ( self yPointAt: 
                                                                  n)) 
                        / (( self xPointAt: n + 1) - ( self xPointAt: 
                                                                  n)) 
                            - ((( self yPointAt: n) - ( self 
                                         yPointAt: n - 1)) / dx)) * 6 
                        * inv2dx - ((u at: n - 1) * s)) 
                        * w].
    endPointDerivative isNil 
        ifTrue: [coefficients at: size put: 0]
        ifFalse: 
            [w := 1 / 2.
            s := 1 / ((self xPointAt:  size) - (self xPointAt: ( size 
                                                               - 1))).
            u at: 1
                put: 3 * s * (endPointDerivative 
                                - (s * (self yPointAt: size) - (self 
                                                yPointAt: size - 1))).
            coefficients at: size
                put: s - (w * (u at: size - 1) / ((coefficients at: 
                                                 size - 1) * w + 1))].
    size - 1 to: 1
        by: -1
        do: 
            [:n | 
            coefficients at: n
                put: (coefficients at: n) * (coefficients at: n + 1) 
                                                          + (u at: n)]

\end{verbatim}
{\bf defaultSamplePoints}
\begin{verbatim}
    ^SortedCollection sortBlock: [ :a :b | a x < b x]

\end{verbatim}
{\bf endPointDerivative:} {\tt aNumber}
\begin{verbatim}
    endPointDerivative := aNumber.
    self resetCoefficients.

\end{verbatim}
{\bf resetEndPointDerivatives}
\begin{verbatim}
    self setEndPointDerivatives: ( Array new: 2).

\end{verbatim}
{\bf setEndPointDerivatives:} {\tt anArray}
\begin{verbatim}
    startPointDerivative := anArray at: 1.
    endPointDerivative := anArray at: 2.
    self resetCoefficients.

\end{verbatim}
{\bf startPointDerivative:} {\tt aNumber}
\begin{verbatim}
    startPointDerivative := aNumber.
    self resetCoefficients.

\end{verbatim}
{\bf value:} {\tt aNumber}
\begin{verbatim}
    | answer n1 n2 n step a b |
    coefficients isNil ifTrue: [self computeSecondDerivatives].
    n2 := pointCollection size.
    n1 := 1.
    [n2 - n1 > 1] whileTrue: 
            [n := (n1 + n2) // 2.
            (self xPointAt:  n) > aNumber ifTrue: [n2 := n] ifFalse: 
                                                           [n1 := n]].
    step := (self xPointAt: n2) - (self xPointAt: n1).
    a := ((self xPointAt: n2) - aNumber) / step.
    b := (aNumber - (self xPointAt: n1)) / step.
    ^a * (self yPointAt: n1) + (b * (self yPointAt: n2)) 
        + ((a * (a squared - 1) * (coefficients at: n1) 
                + (b * (b squared - 1) * (coefficients at: n2))) * 
                                                         step squared 
                / 6)

\end{verbatim}

