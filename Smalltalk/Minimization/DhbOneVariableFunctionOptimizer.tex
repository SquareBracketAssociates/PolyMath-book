$$\halign{ #\hfil&\quad#\hfil\cr {\sl Class}& {\Large\bf DhbOneVariableFunctionOptimizer}\cr
{\sl Subclass of }&{\tt DhbFunctionOptimizer}\cr\noalign{\vskip 1ex}

{\sl Class variable names:}&\parbox[t]{4 in}{\tt  GoldenSection }\cr\noalign{\vskip 1ex}}$$


Class methods
{\parskip 1ex\par\noindent}
{\bf defaultPrecision}
\begin{verbatim}
    ^DhbFloatingPointMachine new defaultNumericalPrecision * 10

\end{verbatim}
{\bf goldenSection}
\begin{verbatim}
    GoldenSection isNil ifTrue: [GoldenSection := (3 - 5 sqrt) / 2].
    ^GoldenSection

\end{verbatim}



Instance methods
{\parskip 1ex\par\noindent}
{\bf computeInitialValues}
\begin{verbatim}
    [ bestPoints size  > 3] whileTrue: [ bestPoints removeLast].
    bestPoints size = 3
        ifTrue: [ self hasBracketingPoints
                    ifFalse:[ bestPoints removeLast].
                ].
    bestPoints size < 3
        ifTrue: [ ( DhbOptimizingBracketFinder forOptimizer: self) 
                                                            evaluate].

\end{verbatim}
{\bf computePrecision}
\begin{verbatim}
    ^self precisionOf: ( ( bestPoints at: 2) position - ( bestPoints 
                                                  at: 3) position) abs
           relativeTo: ( bestPoints at: 1) position abs

\end{verbatim}
{\bf evaluateIteration}
\begin{verbatim}
    self addPointAt: self nextXValue.
    bestPoints removeAtIndex: self indexOfOuterPoint.
    ^self computePrecision

\end{verbatim}
{\bf hasBracketingPoints}
\begin{verbatim}
    | x1 |
    x1 := ( bestPoints at: 1) position.
    ^( ( bestPoints at: 2) position - x1) * (( bestPoints at: 3) 
                                                    position - x1) < 0

\end{verbatim}
{\bf indexOfOuterPoint}
\begin{verbatim}
    | inferior superior x |
    inferior := false.
    superior := false.
    x := bestPoints first position.
    2 to: 4 do:
        [ :n |
          ( bestPoints at: n) position < x
                ifTrue: [ inferior
                            ifTrue: [ ^n].
                          inferior := true.
                        ]
                ifFalse:[ superior
                            ifTrue: [ ^n].
                          superior := true.
                        ].
        ].

\end{verbatim}
{\bf nextXValue}
\begin{verbatim}
    | d3 d2 x1 |
    x1 := ( bestPoints at: 1) position.
    d2 := ( bestPoints at: 2) position - x1.
    d3 := ( bestPoints at: 3) position - x1.
    ^( d3 abs > d2 abs ifTrue: [ d3]
                       ifFalse:[ d2]) * self class goldenSection + x1

\end{verbatim}
{\bf reset}
\begin{verbatim}
    [ bestPoints isEmpty] whileFalse: [ bestPoints removeLast].

\end{verbatim}

